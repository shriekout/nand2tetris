// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/2/ALU.hdl
/**
 * ALU (Arithmetic Logic Unit):
 * Computes out = one of the following functions:
 *                0, 1, -1,
 *                x, y, !x, !y, -x, -y,
 *                x + 1, y + 1, x - 1, y - 1,
 *                x + y, x - y, y - x,
 *                x & y, x | y
 * on the 16-bit inputs x, y,
 * according to the input bits zx, nx, zy, ny, f, no.
 * In addition, computes the two output bits:
 * if (out == 0) zr = 1, else zr = 0
 * if (out < 0)  ng = 1, else ng = 0
 */
// Implementation: Manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) sets x = 0        // 16-bit constant
// if (nx == 1) sets x = !x       // bitwise not
// if (zy == 1) sets y = 0        // 16-bit constant
// if (ny == 1) sets y = !y       // bitwise not
// if (f == 1)  sets out = x + y  // integer 2's complement addition
// if (f == 0)  sets out = x & y  // bitwise and
// if (no == 1) sets out = !out   // bitwise not

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute (out = x + y) or (out = x & y)?
        no; // negate the out output?
    OUT 
        out[16], // 16-bit output
        zr,      // if (out == 0) equals 1, else 0
        ng;      // if (out < 0)  equals 1, else 0

    PARTS:
    // x
    Mux16(a=x, b=false, sel=zx, out=wX);
    Not16(in=wX, out=nwX);
    Mux16(a=wX, b=nwX, sel=nx, out=outX);

    // y
    Mux16(a=y, b=false, sel=zy, out=wY);
    Not16(in=wY, out=nwY);
    Mux16(a=wY, b=nwY, sel=ny, out=outY);

    // x + y
    Add16(a=outX, b=outY, out=addXY);

    // x & y
    And16(a=outX, b=outY, out=andXY);

    // which (f)
    Mux16(a=andXY, b=addXY, sel=f, out=outF);

    // negative?
    Not16(in=outF, out=noutF);
    Mux16(a=outF, b=noutF, sel=no, out=out, 
            out[0..7]=low8bit, out[8..15]=high8bit,
            out[15]=high1bit);

    // zr
    Or8Way(in=low8bit, out=low8);
    Or8Way(in=high8bit, out=high8);
    Or(a=low8, b=high8, out=isZero);
    Not(in=isZero, out=zr);

    // ng
    And(a=high1bit, b=true, out=ng);
}